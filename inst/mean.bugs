model {
    # Priors
    theta ~ dnorm(0, .01)
    precision.tau ~ dgamma(0.001, 0.001)
    tau <- sqrt(1/precision.tau)

    # Likelihood
    for (i in 1:length(m)) {
      m[i] ~ dnorm(mu[i], mu.tau[i])
      mu.tau[i] <- 1/(m.sd[i] * m.sd[i])
      mu[i] ~ dnorm(theta, precision.tau)

      # Assess model fit
      predicted[i] <- mu[i]  # Predicted Values
      residual[i] <- m[i] - predicted[i]  # Residuals for observed data
      sq[i] <- pow(residual[i], 2)  # Squared residuals for observed data

      # Generate Replicate Data and Compute Fit Stats for Them
      m.new[i] ~ dnorm(mu[i], mu.tau[i])  # One new data set at each MCMC iteration
      sq.new[i] <- pow(m.new[i] - predicted[i], 2)  # Squared residuals for new data
    }

    # Assess model fit using a sums-of-squares-type discrepancy
    fit <- sum(sq[])  # Sum of squared residuals for actual data set
    cSD <- sqrt(mean(sq[]))  # Sum of squared residuals for actual data set


    fit.new <- sum(sq.new[])  # Sum of squared residuals for new data set
    test <- step(fit.new - fit)  # Test whether new data set more extreme
    bpvalue <- mean(test)  # Bayesian p-value
}
